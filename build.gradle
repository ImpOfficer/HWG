buildscript {
    repositories {
        jcenter()
    	maven { url  = "https://plugins.gradle.org/m2/" }
    }
}

import groovy.json.JsonSlurper
import groovy.json.JsonOutput
import com.modrinth.minotaur.dependencies.ModDependency

plugins {
    id 'fabric-loom' version '1.1-SNAPSHOT'
	id 'maven-publish'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id "com.modrinth.minotaur" version "2.+"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven { url "https://cfa2.cursemaven.com" }
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.terraformersmc.com/" }
    maven { url 'https://dl.cloudsmith.io/public/azuredoom-mods/azurelib/maven/' }
    maven { url 'https://dl.cloudsmith.io/public/tslat/sbl/maven/' }
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url "https://api.modrinth.com/maven" }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation "net.tslat.smartbrainlib:SmartBrainLib-fabric-1.19.4:1.9"
	modImplementation "net.tslat.smartbrainlib:SmartBrainLib-common-1.19.4:1.9"
  	modImplementation 'mod.azure.azurelib:azurelib-fabric-1.19.4:1.0.26'
	modImplementation ("com.terraformersmc:modmenu:6.1.0-rc.4")
    modCompileOnly("me.shedaniel:RoughlyEnoughItems-api-fabric:11.0.597") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modRuntimeOnly("me.shedaniel:RoughlyEnoughItems-fabric:11.0.597") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modApi("dev.architectury:architectury-fabric:8.1.75") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modApi("me.shedaniel.cloth:cloth-config-fabric:10.0.96") {
        exclude(group: "net.fabricmc.fabric-api")
    }
}

processResources {
    inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

processResources {
	doLast {
        def jsonMinifyStart = System.currentTimeMillis()
        def jsonMinified = 0
        def jsonBytesSaved = 0
        fileTree(dir: outputs.files.asPath, include: '**/*.json').each { 
            File file = it
            jsonMinified++
            def oldLength = file.length()
            file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
            jsonBytesSaved += oldLength - file.length()
        }
        println('Minified ' + jsonMinified + ' json files. Saved ' + jsonBytesSaved + ' bytes. Took ' + (System.currentTimeMillis() - jsonMinifyStart) + 'ms.')
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

jar {
	from "LICENSE"
}

loom {
	accessWidenerPath = file("src/main/resources/hwg.accesswidener")
}

if (file('key.properties').exists()) {
    curseforge {

    	def curseProp = new Properties()
    	File secretPropsFile = file("key.properties")
    	curseProp.load(secretPropsFile.newInputStream())

    	project {
        	apiKey = curseProp.getProperty('curseKey')
        	id = "448106"
        	releaseType = "release"
        	changelogType = 'text'
        	changelog = file('CHANGELOG.txt')
			addGameVersion("1.19.4")
			addGameVersion("Fabric")
			addGameVersion("Quilt")
			addGameVersion("Java 17")

        	relations {
            	requiredDependency 'fabric-api'  
            	requiredDependency 'azurelib'  
            	requiredDependency 'smartbrainlib'  
            	optionalDependency 'modmenu' 
        	}
        	mainArtifact(remapJar)

        	afterEvaluate {
            	uploadTask.dependsOn("remapJar")
        	}
    	}
    	options {
    		//debug = true
        	forgeGradleIntegration = false
    	}
    }
}

if (file('key.properties').exists()) {
	modrinth {
		def modrinthProp = new Properties()
		File secretPropsFile = file("key.properties")
		modrinthProp.load(secretPropsFile.newInputStream())
		
		token = modrinthProp.getProperty('modrinthKey')
		projectId = 'SPMW1tzD' 
		versionNumber = project.mod_version 
		versionName = '1.19.4'
		uploadFile = remapJar 
		changelog = rootProject.file("changelog.txt").text
		gameVersions = ['1.19.4']
		loaders = ['fabric', 'quilt']
		dependencies { 
        	required.project "azurelib"
        	required.project "smartbrainlib"
        	required.project "fabric-api"
            optional.project "modmenu"
   		}
	}
}